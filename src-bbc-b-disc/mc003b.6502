;		TTL	e.mc003b	; 2nd half of game handler

;*****************************************
;*	CHECK  FOR  HITTING  VERTICALS   *
;*****************************************

		LDA	xy+2
		STA	mem+2

		LDA	xy+3
		STA	mem+3

		LDA	xy
		AND	#63
		CMP	#32
		BCS	NL890		; Which vert to check???
		JMP	L890

.NL890		LDA	xy
;*		CLC
		ADC	#14		; WAS 15
		STA	mem

		LDA	xy+1
		ADC	#0
		STA	mem+1

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#16
		STA	hites
		BCS	J890

		LDA	hites
		CMP	workspace+&F
		BCS	J890

		JSR	bounce

		LDA	oxy
		SEC
		SBC	#8
		STA	xy

		LDA	oxy+1
		SBC	#0
		STA	xy+1

		LDA	ab+1
		BMI	J890

		LDA	ab
		EOR	#255
		CLC
		ADC	#1
		STA	ab

		LDA	ab+1
		EOR	#255
		ADC	#0
		STA	ab+1

		JMP	J890

;	**************************

.L890		LDA	xy
		SEC
		SBC	#15
		STA	mem

		LDA	xy+1
		SBC	#0
		STA	mem+1

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#16
		STA	hites
		BCS	J890

		LDA	hites
		CMP	workspace+&F
		BCS	J890

		JSR	bounce

		LDA	oxy
		CLC
		ADC	#8
		STA	xy

		LDA	oxy+1
		ADC	#0
		STA	xy+1

		LDA	ab+1
		BPL	J890

		LDA	ab
		EOR	#255
		CLC
		ADC	#1
		STA	ab

		LDA	ab+1
		EOR	#255
		ADC	#0
		STA	ab+1

;	*************************


.J890		LDA	xy
		STA	mem

		LDA	xy+1
		STA	mem+1

		LDA	xy+2
		AND	#63
		CMP	#32
		BCC	skip_900	; Which vert to check???
		JMP	L900

.skip_900	LDA	xy+2
;*		SEC
		SBC	#14		; WAS 15
		STA	mem+2

		LDA	xy+3
		SBC	#0
		STA	mem+3

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#16
		STA	hites
		BCS	DC1

		LDA	hites
		CMP	workspace+&F
		BCS	DC1

		JSR	bounce

		LDA	oxy+2
		CLC
		ADC	#8
		STA	xy+2

		LDA	oxy+3
		ADC	#0
		STA	xy+3

		LDA	ab+3
		BPL	DC1

		LDA	ab+2
		EOR	#255
;*		CLC
		ADC	#1
		STA	ab+2

		LDA	ab+3
		EOR	#255
		ADC	#0
		STA	ab+3

		JMP	DC1

;	***************************


.L900		LDA	xy+2
;*		CLC
		ADC	#14		; WAS 15
		STA	mem+2

		LDA	xy+3
		ADC	#0
		STA	mem+3

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#16
		STA	hites
		BCS	DC1

		CMP	workspace+&F
		BCS	DC1

		JSR	bounce

		LDA	oxy+2
		SEC
		SBC	#8
		STA	xy+2

		LDA	oxy+3
		SBC	#0
		STA	xy+3

		LDA	ab+3
		BMI	DC1

		LDA	ab+2
		EOR	#255
;*		CLC
		ADC	#0		; WAS 1
		STA	ab+2

		LDA	ab+3
		EOR	#255
		ADC	#0
		STA	ab+3


;	*****************************************
;	*	  DIAGONAL VERT. CHECKS		*
;	*****************************************
.DC1
		LDA	xy		; Diagonal check #1
		AND	#63
		CMP	#32
		BCC	DC2

		LDA	xy
;*		CLC
		ADC	#11		; WAS 12
		STA	mem

		LDA	xy+1
		ADC	#0
		STA	mem+1

		LDA	xy+2
		AND	#63
		CMP	#32
		BCS	DC2

		LDA	xy+2
;*		SEC
		SBC	#11		; WAS 12
		STA	mem+2

		LDA	xy+3
		SBC	#0
		STA	mem+3

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#20
		BCC	J1
		JMP	L910

.J1		CMP	workspace+&F
		BCC	J2
		JMP	L910

.J2		JSR	bounce

		LDA	oxy
		SEC
		SBC	#8
		STA	xy

		LDA	oxy+1
		SBC	#0
		STA	xy+1

		LDA	oxy+2
;*		CLC
		ADC	#7		; WAS 8
		STA	xy+2

		LDA	oxy+3
		ADC	#0
		STA	xy+3

		LDA	ab
		BMI	negpos1

		EOR	#255		
		STA	ab

		LDA	ab+1
		EOR	#255
		STA	ab+1

.negpos1	LDA	ab+2
		BMI	isneg
		JMP	L910

.isneg		EOR	#255
		STA	ab+2

		LDA	ab+3
		EOR	#255
		STA	ab+3

		JMP	L910

;	**************************

.DC2		LDA	xy		; Diagonal check #2
		AND	#63
		CMP	#32
		BCS	DC3

		LDA	xy
;*		SEC
		SBC	#11		; WAS 12
		STA	mem

		LDA	xy+1
		SBC	#0
		STA	mem+1

		LDA	xy+2
		AND	#63
		CMP	#32
		BCC	DC3

		LDA	xy+2
;*		CLC
		ADC	#11		; WAS 12
		STA	mem+2

		LDA	xy+3
		ADC	#0
		STA	mem+3

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#20
		BCC	J3
		JMP	L910

.J3		CMP	workspace+&F
		BCC	J4
		JMP	L910


.J4		JSR	bounce

		LDA	oxy
		CLC
		ADC	#8
		STA	xy

		LDA	oxy+1
		ADC	#0
		STA	xy+1

		LDA	oxy+2
;*		SEC
		SBC	#7		; WAS 8
		STA	xy+2

		LDA	oxy+3
		SBC	#0
		STA	xy+3

		LDA	ab
		BPL	negpos2

		EOR	#255		
		STA	ab

		LDA	ab+1
		EOR	#255
		STA	ab+1

.negpos2	LDA	ab+2
		BPL	J5
		JMP	L910

.J5		EOR	#255
		STA	ab+2

		LDA	ab+3
		EOR	#255
		STA	ab+3

		JMP	L910

;	**************************

.DC3		LDA	xy		; Diagonal check #3
		AND	#63
		CMP	#32
		BCC	DC4

		LDA	xy
;*		CLC
		ADC	#11		; WAS 12
		STA	mem

		LDA	xy+1
		ADC	#0
		STA	mem+1

		LDA	xy+2
		AND	#63
		CMP	#32
		BCC	DC4

		LDA	xy+2
;*		CLC
		ADC	#11		; WAS 12
		STA	mem+2

		LDA	xy+3
		ADC	#0
		STA	mem+3

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#20
		BCC	L1
		JMP	L910

.L1		CMP	workspace+&F
		BCC	L2
		JMP	L910

.L2		JSR	bounce

		LDA	oxy
		SEC
		SBC	#8
		STA	xy

		LDA	oxy+1
		SBC	#0
		STA	xy+1

		LDA	oxy+2
		SBC	#8
		STA	xy+2

		LDA	oxy+3
		SBC	#0
		STA	xy+3

		LDA	ab
		BMI	negpos3

		EOR	#255		
		STA	ab

		LDA	ab+1
		EOR	#255
		STA	ab+1

.negpos3	LDA	ab+2
		BPL	L3
		JMP	L910

.L3		EOR	#255
		STA	ab+2

		LDA	ab+3
		EOR	#255
		STA	ab+3

		JMP	L910

;	**************************

.DC4		LDA	xy		; Diagonal check #4
		AND	#63
		CMP	#32
		BCS	L910

		LDA	xy
;*		SEC
		SBC	#11		; WAS 12
		STA	mem

		LDA	xy+1
		SBC	#0
		STA	mem+1

		LDA	xy+2
		AND	#63
		CMP	#32
		BCS	L910

		LDA	xy+2
;*		SEC
		SBC	#11		; WAS 12
		STA	mem+2

		LDA	xy+3
		SBC	#0
		STA	mem+3

		JSR	hi

		LDA	xy+4
		CLC
		ADC	#20
		BCS	L910

		CMP	workspace+&F
		BCS	L910

		JSR	bounce

		LDA	oxy
		CLC
		ADC	#8
		STA	xy

		LDA	oxy+1
		ADC	#0
		STA	xy+1

		LDA	oxy+2
		ADC	#8
		STA	xy+2

		LDA	oxy+3
		ADC	#0
		STA	xy+3

		LDA	ab
		BPL	negpos4

		EOR	#255		
		STA	ab

		LDA	ab+1
		EOR	#255
		STA	ab+1

.negpos4	LDA	ab+2
		BPL	L910

		EOR	#255
		STA	ab+2

		LDA	ab+3
		EOR	#255
		STA	ab+3



;****************************************

.L910		LDA	ab
		BPL	snneg

		EOR	#255

.snneg		LSR	A
		LSR	A
		LSR	A
		CMP	#15
		BCC	lt15

		LDA	#15

.lt15		STA	x_bar


		LDA	ab+2
		BPL	snneg2

		EOR	#255

.snneg2		LSR	A
		LSR	A
		LSR	A
		CMP	#15
		BCC	lt152

		LDA	#15

.lt152		STA	y_bar


		LDA	xy+4
		LSR	A
		LSR	A
		LSR	A
		LSR	A
		STA	h_bar

;****************************************

		LDA	xy+4
		BNE	L920

		JMP	wipeout		; Deceased



;	*****************************************
;	*      The     One     and     Only	*
;	*	 Hidden    Ball   Removal	*
;	*	  Co-Ordination  Routine	*
;	*****************************************

.L920		LDX	#7

.vlp3		LDA	ball_mask,X	; copy frame ( from <ball_mask> data )
		STA	c+1,X		; into c+1 to c+8
		DEX
		BPL	vlp3

		LDA	#127		; ?workspace+&D = 127	(mask)
		STA	workspace+&D

		LDA	#0
		STA	drmem+7		; ?drmem+7 = 0		(pixel counter)
		BEQ	noror		; WAS BRA

.d22		SEC
		ROR	workspace+&D

.noror		LDA	#0
		STA	drmem+5		; ?drmem+5 = 0		(distance searched)

		LDA	drmem+7		; ?drmem+8 = ?drmem+7 -5
		SEC
		SBC	#5
		STA	drmem+8

		LDA	#0
		SBC	#0
		ASL	drmem+8
		ROL	A
		STA	drmem+9

		LDA	xy		; Set up X Co_Ord for scan
		CLC
		ADC	drmem+8
		STA	drmem

		LDA	xy+1
		ADC	drmem+9
		STA	drmem+1

		LDA	xy+2		; Set Y co_ord
		ADC	drmem+8
		STA	drmem+2

		LDA	xy+3
		ADC	drmem+9
		STA	drmem+3

.MGM		LDA	drmem		; If not block edge,skip check
		AND	#63
		BEQ	cF

		LDA	drmem+2
		AND	#63
		CMP	#63
		BEQ	cF

		JMP	e

.cF


;****************************************

.Dhi		LDA	drmem			; Shortened 'hi' routine
		STA	workspace+&8
		LDA	drmem+1
		BPL	nneg1

		LDA	#0
		JMP	finDhi

.nneg1		ASL	workspace+&8
		ROL	A
		ASL	workspace+&8
		ROL	A
		TAX

		LDA	drmem+2
		STA	workspace+&8
		LDA	drmem+3
		BPL	nneg2

		LDA	#0
		JMP	finDhi

.nneg2		ASL	workspace+&8
		ROL	A
		ASL	workspace+&8
		ROL	A
		TAY

		CPX	#10
		BMI	Dno1
		LDA	#0
		JMP	finDhi

.Dno1		CPY	#10
		BMI	Dno2
		LDA	#0
		JMP	finDhi

.Dno2		STX	workspace+&10
		STY	workspace+&11
		LDX	#11
		JSR	mult
		CLC
		ADC	workspace+&10
		TAY
		LDA	(pointer),Y
		STA	workspace
		TAX

		AND	#1
		BNE	Dno3
		LDA	#0
		JMP	finDhi

.Dno3		TXA
		AND	#2
		BEQ	Dnflt2
		TXA
		AND	#252
		JMP	finDhi

.Dnflt2		TXA
		AND	#252
		STA	workspace
		TYA
		CLC
		ADC	#11
		TAY
		LDA	(pointer),Y
		AND	#252
		STA	workspace+&2
		INY
		LDA	(pointer),Y
		AND	#252
		STA	workspace+&3

		LDA	#0
		STA	workspace+&8
		STA	workspace+&9
		LDA	drmem		; Is it left of block(X=0)?
		AND	#63
		BNE	Dno99

		LDA	workspace+&2	; Find Gradient
		SEC
		SBC	workspace
		STA	workspace+&4
		LDA	#0
		SBC	#0
		STA	workspace+&5

		LDA	drmem+2
		AND	#63
		LDX	#6
.Dlop99		LSR	A
		BCC	DNCYET
		STA	workspace+&F
		LDA	workspace+&8
		CLC
		ADC	workspace+&4
		STA	workspace+&8
		LDA	workspace+&9
		ADC	workspace+&5
		STA	workspace+&9
		LDA	workspace+&F
.DNCYET		ASL	workspace+&4
		ROL	workspace+&5
		DEX
		BNE	Dlop99

		ASL	workspace+&8
		ROL	workspace+&9
		ASL	workspace+&8
		ROL	workspace+&9

		LDA	workspace
		CLC
		ADC	workspace+&9
		JMP	finDhi

.Dno99


		LDA	workspace+&3	; Find Gradient
		SEC
		SBC	workspace+&2
		STA	workspace+&4
		LDA	#0
		SBC	#0
		STA	workspace+&5

		LDA	drmem
		AND	#63
		LDX	#6
.Elop99		LSR	A
		BCC	ENCYET
		STA	workspace+&F
		LDA	workspace+&8
		CLC
		ADC	workspace+&4
		STA	workspace+&8
		LDA	workspace+&9
		ADC	workspace+&5
		STA	workspace+&9
		LDA	workspace+&F
.ENCYET		ASL	workspace+&4
		ROL	workspace+&5
		DEX
		BNE	Elop99

		ASL	workspace+&8
		ROL	workspace+&9
		ASL	workspace+&8
		ROL	workspace+&9

		LDA	workspace+&2
		CLC
		ADC	workspace+&9

.finDhi
;****************************************


		LSR	A
		LSR	A
		STA	mem

		LDA	drmem+5
		LSR	A
		LSR	A
		CLC
		ADC	drmem+5
		TAX

		LDA	#0
		ADC	#0
		STA	hites+1

		TXA
		ADC	xy+4
		STA	hites
		BCC	nrp

		INC	hites+1

.nrp		LDA	#7
		STA	workspace+&C

.loopfd		LDA	#6
		SEC
		SBC	workspace+&C
		STA	drmem+6

		LDA	#0
		SBC	#0
		ASL	drmem+6
		ROL	A
		ASL	drmem+6
		ROL	A
		STA	drmem+4

		LDA	drmem+6
		CLC
		ADC	hites
		STA	drmem+6

		LDA	drmem+4
		ADC	hites+1
		LSR	A
		ROR	drmem+6
		LSR	A
		STA	drmem+4
		ROR	drmem+6


		LDA	drmem+4			; ***
		BNE	e

		LDA	mem
		CMP	drmem+6
		BMI	e
		BEQ	e

.smallr		LDX	workspace+&C
		LDA	c+1,X
		AND	workspace+&D
		STA	c+1,X

		DEC	workspace+&C
		LDX	workspace+&C
		CPX	#7
		BCS	e

		JMP	loopfd

.e		LDA	drmem
		AND	#63
		EOR	#63
		STA	drmem+6

		LDA	drmem+2
		AND	#63
		CMP	drmem+6
		BCS	jr

		STA	drmem+6

.jr		INC	drmem+6
		LDA	drmem+5
		CLC
		ADC	drmem+6
		STA	drmem+5

		LDA	drmem
		CLC
		ADC	drmem+6
		STA	drmem
		BCC	NOKOT

		INC	drmem+1

.NOKOT		LDA	drmem+2
		SEC
		SBC	drmem+6
		STA	drmem+2

		LDA	drmem+3
		SBC	#0
		STA	drmem+3

		LDA	drmem+5
		CMP	#180		; Is scan outa range???
		BCS	longwy

		JMP	MGM

;****************************************
;****************************************

;		INCLUDE	"mc003c"
