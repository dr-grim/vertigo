;		TTL	mc998		; end of level animation bits

;****************************************
;****************************************

dea_frcount	=	drmem
lb_frame	=	drmem+1

;****************************************

.eolevel5	LDX	#LO(levmes5)
		LDY	#HI(levmes5)
		JSR	show_mes

		LDA	#1
		STA	indicator
		JSR	doscreen

		LDA	#LO(final)
		STA	restore+1
		LDA	#HI(final)
		STA	restore+2
		LDA	#28
		STA	ra_height+1
		LDA	#3
		LDX	#25
		LDY	#56
		JSR	restore_area

		JSR	reset_cols

		LDA	#64		; X co-ord
		STA	xy
		LSR	A
		STA	xy+2


		LDX	#1
		STX	xy+1

		DEX
		STX	xy+3

		LDA	#25		; height
		STA	xy+4

		JSR	setup_ball

		JSR	tel2

; * * * * * * * * * * * * * * * * * * * *

.f_lp		JSR	anim_doframe

		LDA	lb_frame
		LDX	#21
		LDY	#61
		JSR	disp_lball

		LDA	lb_frame
		EOR	#15
		LDX	#27
		LDY	#88
		JSR	disp_lball

		LDA	xy+2
		CLC
		ADC	#3
		STA	xy+2
		LDA	xy+3
		ADC	#0
		STA	xy+3

		CMP	#2
		BLT	f_lp

		LDA	xy+2
		CMP	#10
		BLT	f_lp

; * * * * * * * * * * * * * * * * * * * *

		JSR	ting		; Hit end : ie. now teleport out

		LDA	#4
		STA	skip_splat2+1

		JSR	no_skip_splat

		LDA	#8
		STA	skip_splat2+1

		JSR	venetian

		LDA	#2
		JSR	doscreen

		JSR	reset_cols

		LDX	#1
		STX	indicator
		INX
		STX	xy+1
		STX	xy+3

		LDA	#&20		; X co-ord
		STA	xy
		STA	xy+2

		LDA	#12		; height
		STA	xy+4

		JSR	tel2

.f_lp2		LDA	#1
		JSR	kr2

		LDA	xy+2
		SEC
		SBC	#3
		STA	xy+2
		STA	xy
		LDA	xy+3
		SBC	#0
		STA	xy+3
		STA	xy+1

		BNE	f_lp2

		LDA	xy+2
		BMI	f_lp2

		LDA	#0
		LDX	#LO(bsp2)
		LDY	#HI(bsp2)
		JSR	speech_bub

		JSR	keep_rot

		LDA	#0
		LDX	#LO(bsp1)
		LDY	#HI(bsp1)
		JSR	speech_bub

		LDA	#1
		LDX	#LO(lbsp2)
		LDY	#HI(lbsp2)
		JSR	speech_bub

		JSR	keep_rot

		LDA	#0
		LDX	#LO(bsp3)
		LDY	#HI(bsp3)
		JSR	speech_bub

		LDA	#1
		LDX	#LO(lbsp1)
		LDY	#HI(lbsp1)
		JSR	speech_bub

		JSR	keep_rot

		LDA	#0
		LDX	#LO(bsp1)
		LDY	#HI(bsp1)
		JSR	speech_bub

		LDA	#1
		LDX	#LO(lbsp3)
		LDY	#HI(lbsp3)
		JSR	speech_bub

		JSR	keep_rot

		LDA	#0
		LDX	#LO(bsp4)
		LDY	#HI(bsp4)
		JSR	speech_bub

		LDA	#1
		LDX	#LO(lbsp1)
		LDY	#HI(lbsp1)
		JSR	speech_bub

		JSR	keep_rot

		LDA	#0
		LDX	#LO(bsp5)
		LDY	#HI(bsp5)
		JSR	speech_bub

		JSR	keep_rot

		LDA	#0
		LDX	#LO(bsp1)
		LDY	#HI(bsp1)
		JSR	speech_bub

.f_lp4		LDA	#1
		JSR	kr2

		LDA	xy+2
		CLC
		ADC	#3
		STA	xy+2
		STA	xy
		LDA	xy+3
		ADC	#0
		STA	xy+3
		STA	xy+1

		CMP	#2
		BNE	f_lp4

		LDA	xy+2
		CMP	#&20
		BLT	f_lp4

		JMP	nls3

;****************************************
;****************************************

.tel2		JSR	ting

		JSR	convc

		JMP	no_skip_tele

;****************************************
;****************************************

.speech_bub	STX	restore+1
		STY	restore+2

		PHA
		CMP	#0
		BNE	lb_sb

		LDX	#4
		LDY	#24
		BNE	do_bub

.lb_sb		LDX	#5
		LDY	#32

.do_bub		STY	ra_height+1

		PLA
		TAY

		TXA

		CPY	#0
		BNE	lb_sb2

		LDX	#8
		LDY	#118
		BNE	do_bub2

.lb_sb2		LDX	#1
		LDY	#107

.do_bub2	JMP	restore_area

;****************************************
;****************************************

.keep_rot	LDA	#100
.kr2		STA	dea_frcount

.f_lp3		JSR	anim_doframe

		LDA	lb_frame
		LDX	#5
		LDY	#140
		JSR	disp_lball

		DEC	dea_frcount
		BNE	f_lp3

		RTS

;****************************************
;****************************************

.disp_lball	STA	dlb_pla+1
		TXA
		PHA
		TYA
		PHA

		LDA	#16
		STA	ms_height+1

		LDA	#LO(lb_mask)
		STA	ms_mask+1
		LDA	#HI(lb_mask)
		STA	ms_mask+2

		LDA	#LO(lb_sprt)
		STA	ms_sprite+1
		LDA	#HI(lb_sprt)
		STA	ms_sprite+2

		LDA	#LO(&B000)
		STA	save+1
		LDA	#HI(&B000)
		STA	save+2

.dlb_pla	LDA	#&FF
		BEQ	skp_lb

		TAX

.lb_l		LDA	ms_sprite+1
		CLC
		ADC	#&20
		STA	ms_sprite+1
		BCC	lb_lcc

		INC	ms_sprite+2

.lb_lcc		DEX
		BNE	lb_l

.skp_lb		PLA
		TAY
		PLA
		TAX

		LDA	#2
		JMP	mask_sprite

;****************************************
;****************************************

.anim_doframe	JSR	incnb
		JSR	inclb

		JSR	setup_ball

		JSR	conv_sprite

		LDA	indicator
		BEQ	no_skipX

		LDA	#0
		STA	indicator
		STA	frame
		STA	lb_frame

		JSR	convc2

		JMP	skip_1st_halfX

; * * * * * * * * * * * * * * * * * * * *

.no_skipX	JSR	fx19
		JSR	osbyte		; HERE

		JSR	d_shadow	; delete shadow

		JSR	do_ra

; * * * * * * * * * * * * * * * * * * * *

.skip_1st_halfX	JSR	convc

		JSR	do_ms

		LDA	old_Y
		CLC
		ADC	#8
		STA	old_Y2

		JSR	sp_shadow

		JSR	convcm
		STA	xy+4

; * * * * * * * * * * * * * * * * * * * *

		RTS

;****************************************
;****************************************

.reset_cols	LDA	#7
		JMP	vdu19

;****************************************
;****************************************
;*
;*print_hex	PHA
;*
;*		AND	#&F0
;*		LSR	A
;*		LSR	A
;*		LSR	A
;*		LSR	A
;*		JSR	phd
;*
;*		PLA
;*		AND	#&0F
;*		JSR	phd
;*
;*		LDA	#32
;*		JMP	oswrch
;*
;*phd		CMP	#10
;*		BLT	nl
;*
;*		CLC
;*		ADC	#55
;*		BRA	phs
;*
;*nl		CLC
;*		ADC	#48
;*
;*phs		JMP	oswrch
;*
;****************************************
;****************************************

.inclb		LDX	lb_frame
		DEX
		BPL	lb_ok

		LDX	#15

.lb_ok		STX	lb_frame
		RTS

;****************************************
;****************************************

.incnb		LDX	frame
		DEX
		BPL	nfcl

		LDX	#frames-1

.nfcl		STX	frame
		RTS

;****************************************
;****************************************

.doscreen	STA	screen_number

		LDA	#6
		STA	level_number

		JMP	screen

;****************************************
;****************************************

		INCLUDE	"mc999.6502"
