;		TTL	mc005a		; Takes care of music

;****************************************
;****************************************

.note		EQUB	&13,0,&FF,0,&FF,0,&FF,0

.note0		EQUB	0,&01,6,0,3,0,&FF,0
.note1		EQUB	1,&01,0,0,&FF,0,&FF,0
.note2		EQUB	2,&00,7,0,&FF,0,&FF,0

;****************************************
;****************************************

.setup_main	LDA	#0
		STA	main_offset
		STA	mb_num

		LDA	#LO(mbar0)
		STA	main_pointer
		LDA	#HI(mbar0)
		STA	main_pointer+1

		RTS

;****************************************
;****************************************

.eomainbar	INC	mb_num
		STZ	main_offset

		LDX	mb_num
		LDA	main_bars,X
		CMP	#255
		BNE	m_notend

		JSR	setup_main
		BRA	do_main

.m_notend	ASL	A
		TAX

		LDA	mbars_table,X
		STA	main_pointer
		LDA	mbars_table+1,X
		STA	main_pointer+1

;****************************************
;****************************************

.do_main	LDY	main_offset
		LDA	(main_pointer),Y
		BNE	not_eomain

		INY
		LDA	(main_pointer),Y
		BEQ	eomainbar
		DEY

.not_eomain	LDA	(main_pointer),Y
		CMP	#10		
		BLT	special_main

		STA	note2+4

		INY
		LDA	(main_pointer),Y
		STA	note2+6

		LDA	#7
		LDX	#LO(note2)
		LDY	#HI(note2)
		JSR	osword

		INC	main_offset
		INC	main_offset
		RTS

;****************************************
;****************************************

.special_main	LDA	#0
		STA	note2+2
		INY
		LDA	(main_pointer),Y
		STA	note2+6

		LDA	#7
		LDX	#LO(note2)
		LDY	#HI(note2)
		JSR	osword

		LDA	#7
		STA	note2+2

		INC	main_offset
		INC	main_offset
		RTS

;****************************************
;****************************************

.code
		LDA	#128		; check channel 1
		LDX #-6 AND &FF
		LDY	#0		; spaces (]X)
		JSR	osbyte

		CPX	#4
		BLT	eobass

		JSR	do_bass

		BRA	code

.eobass		LDA	#128		; check channel 1
		LDX #-7 AND &FF
		LDY	#0		; spaces (]X)
		JSR	osbyte

		CPX	#4
		BLT	eomain

		JSR	do_main

		BRA	eobass

.eomain		RTS

;****************************************
;****************************************

.zzap		LDA	#5
		STA	workspace
		STZ	workspace+1

.z_loop1	LDA	&FE62
		EOR	&FE63
		EOR	&FE65
		EOR	&FE66
		EOR	&FE69
		EOR	&FE6A
		EOR	&FE6B
		EOR	&FE6C
		EOR	&FE6D
		EOR	&FE6E
		EOR	&FE19
		EOR	&FE1A

		JSR	noise

		DEC	workspace+1
		BNE	z_loop1

		DEC	workspace
		BNE	z_loop1

		JMP	wipeout

;****************************************
;****************************************

.noise		TAX
		LDA #-13 AND &FF
		LDY	#0
		BRA	sound_effect

;****************************************
;****************************************

.bounce		LDA	#3
		LDX	#20
		LDY	#4
		BRA	sound_effect

;****************************************
;****************************************

.ting		LDA	#4
		LDX	#240
		LDY	#4
		BRA	sound_effect

;****************************************
;****************************************

.ting_v2	LDA	#4
		LDX	#190
		LDY	#10
		BRA	sound_effect

;****************************************
;****************************************

.paff		LDA	#3
		LDX	#200
		LDY	#3

		BRA	sound_effect

;****************************************
;****************************************

.piff		LDA	#4
		LDX	#200
		LDY	#3

;****************************************
;****************************************

.sound_effect	STA	note+2
		STX	note+4
		STY	note+6

		LDA	#7
		LDX	#LO(note)
		LDY	#HI(note)
		JMP	osword

;****************************************
;****************************************

.init_music	JSR	setup_bass
		JSR	setup_main

		JSR	music_onoff

		LDA	#15
		LDX	#0
		JSR	osbyte

		RTS

;****************************************
;****************************************

.setup_bass	LDA	#0
		STA	bass_offset
		STA	bb_num

		LDA	#LO(bbar8)
		STA	bass_pointer
		LDA	#HI(bbar8)
		STA	bass_pointer+1

		RTS

;****************************************
;****************************************

.eobassbar	INC	bb_num
		STZ	bass_offset

		LDX	bb_num
		LDA	bass_bars,X
		CMP	#255
		BNE	b_notend

		JSR	setup_bass
		BRA	do_bass

.b_notend	ASL	A
		TAX

		LDA	bbars_table,X
		STA	bass_pointer
		LDA	bbars_table+1,X
		STA	bass_pointer+1

;****************************************
;****************************************

.do_bass	LDY	bass_offset
		LDA	(bass_pointer),Y
		BNE	not_eobass

		INY
		LDA	(bass_pointer),Y
		BEQ	eobassbar
		DEY

.not_eobass	LDA	(bass_pointer),Y
		CMP	#10		
		BLT	special_bass

		SEC
		SBC	#6
		STA	note1+4

.cont_bass	INY
		LDA	(bass_pointer),Y
		STA	note0+6
		STA	note1+6

.sound_bass	LDA	#7
		LDX	#LO(note1)
		LDY	#HI(note1)
		JSR	osword

		LDA	#7
		LDX	#LO(note0)
		LDY	#HI(note0)
		JSR	osword

.exit_bass	INC	bass_offset
		INC	bass_offset
		RTS

;****************************************
;****************************************

.special_bass	CMP	#4
		BNE	sb_not4

		LDX	#6
		LDY	#7
		BRA	fix_bass

.sb_not4	CMP	#5
		BNE	sb_not5

		LDX	#6
		LDY	#3
		BRA	fix_bass

.sb_not5	CMP	#6
		BNE	sb_not6

		LDX	#10
		LDY	#3
.fix_bass	STX	note0+2
		STY	note0+4
		BRA	exit_bass

.sb_not6	CMP	#1
		BNE	sb_not1

		LDA	note1+4
		PHA
		LDA	note0+2
		PHA

		LDA	#198
		LDX	#8
		BRA	cont_bass2

.sb_not1	CMP	#3
		BNE	sb_not3

		LDA	note1+4
		PHA
		LDA	note0+2
		PHA
		LDA	#220
		LDX	#8
		BRA	cont_bass2

.sb_not3	CMP	#0
		BNE	sb_not0

		LDA	note1+4
		PHA
		LDA	note0+2
		PHA
		LDA	#190
		LDX	#9
		BRA	cont_bass2

.sb_not0	CMP	#2
		BNE	sb_not2

		LDA	note1+4
		PHA
		LDA	note0+2
		PHA
		LDA	#0
		LDX	#0

.cont_bass2	STA	note1+4
		STX	note0+2
		LDA	#7
		STA	note0+4

		JSR	cont_bass

		LDA	#3
		STA	note0+4
		PLA
		STA	note0+2
		PLA
		STA	note1+4

.sb_not2	RTS

;****************************************
;****************************************

;		CHN	mc005b
